#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use Perun::Agent;
use Perun::Common qw(printMessage);
use Switch; 

sub help {
return qq{
Sets the resource-group attribute. Group id or group name and VO, resource id and attribute id or name and attribute value are required fields.
---------------------------------------------------------------
Available options:
 --groupId        | -g group id
 --groupName      | -G group name
 --voId           | -v vo id
 --voShortName    | -V vo short name
 --resourceId     | -r resource id
 --attributeId    | -a attribute id
 --attributeName  | -A attribute name including namespace
 --attributeValue | -w attribute value
 --batch          | -b batch
 --help           | -h prints this help

};
}

my ($groupId, $groupName, $voId, $voShortName, $resourceId,$attributeId, $attributeName, @attributeValue);
our $batch;
GetOptions ("help|h" => sub { print help(); exit 0;} ,"batch|b" => \$batch,
   "groupId|g=i" => \$groupId,
   "groupName|G=s" => \$groupName,
   "voId|v=i" => \$voId,
   "voShortName|V=s" => \$voShortName,
   "resourceId|r=i" => \$resourceId,
   "attributeId|a=i" => \$attributeId, 
   "attributeName|A=s" => \$attributeName, 'attributeValue|w=s@{1,}' => \@attributeValue) || die help(); 



# Check options
unless (defined($groupId) or ((defined($voShortName) or defined($voId)) and defined($groupName))) {die "ERROR: groupId or groupName and voId or voShortName is required\n";}
unless (defined($resourceId)) { die "ERROR: resourceId is required \n";}
unless (defined($attributeId) or defined($attributeName)) { die "ERROR: attributeId or attributeName is required \n";}
unless (@attributeValue) { die "ERROR: attributeValue is required \n";}

my $agent = Perun::Agent->new();
my $vosAgent = $agent->getVosAgent;
my $groupsAgent = $agent->getGroupsAgent;

unless(defined($groupId)) {
    unless (defined($voId)) {
        my $vo = $vosAgent->getVoByShortName(shortName => $voShortName);
        $voId = $vo->getId;
  }

  my $group = $groupsAgent->getGroupByName(vo => $voId, name => $groupName);
  $groupId = $group->getId;
}

my $attributesAgent = $agent->getAttributesAgent;
my $attributeDefinition;

if (!defined($attributeId)) {
  $attributeDefinition =  $attributesAgent->getAttributeDefinition(attributeName => $attributeName);
} else {
  $attributeDefinition =  $attributesAgent->getAttributeDefinitionById(id => $attributeId);
}

# Get the attribute definition and create the attribute
my $attribute = Perun::beans::Attribute->fromAttributeDefinition($attributeDefinition);

$attribute->setValueFromArray(@attributeValue);

$attributesAgent->setAttribute(group => $groupId, resource => $resourceId, attribute => $attribute);

printMessage("Attribute Id:".$attribute->getId." set for the group Id:$groupId", $batch);
