#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use Text::ASCIITable;
use Perun::Agent;
use Perun::Common qw(printMessage tableToPrint getSortingFunction); 

sub help {
return qq{
Prints list of Resources assigned to Facility. Facility is required field.
---------------------------------------------------
Available options:
 --facilityId   | -f  facility identifier 
 --facilityName | -F  facility name
 --facilityType | -t  facility type
 --orderById    | -i  order by numeric Id
 --orderByName  | -n  order by name (default)
 --batch        | -b  batch
 --help         | -h  prints this help
};
}

my ($facilityId, $facilityName, $facilityType, $batch, $sortingFunction);
GetOptions("help|h" => sub { print help; exit 0;} ,
           "facilityId|f=i" => \$facilityId,
           "facilityName|F=s" => \$facilityName,
           "facilityType|t=s" => \$facilityType,
           "orderById|i" => sub { $sortingFunction = getSortingFunction("getId") } ,
           "orderByName|n" =>  sub {$sortingFunction = getSortingFunction("getName", 1) }, 
           "batch|b" => \$batch) || die help;

#options check
unless(defined $facilityId or (defined $facilityName and defined $facilityType)) { die "ERROR: facilityId  or facilityName+facilityType is required\n";} 
unless(defined $sortingFunction) { $sortingFunction = getSortingFunction("getName", 1); }

my $agent = Perun::Agent->new();
my $facAgent = $agent->getFacilitiesAgent;
my $resourceAgent = $agent->getResourcesAgent;

unless($facilityId) {
    my $facility = $facAgent->getFacilityByName(name => $facilityName, type => $facilityType);
    $facilityId = $facility->getId;
}

my @resources = $facAgent->getAssignedResources(facility => $facilityId);
unless(@resources) { printMessage "No Resources assigned", $batch; exit 0; }

#output
my $table = Text::ASCIITable->new();
$table->setCols('ID','Name');

foreach my $resource (sort $sortingFunction @resources) {
  $table->addRow($resource->getId, $resource->getName);
}

print tableToPrint($table, $batch);
