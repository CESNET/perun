#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use Text::ASCIITable;
use Perun::Agent;
use Perun::Common qw(printMessage tableToPrint getSortingFunction); 

sub help {
return qq{
Tool prints list of Resources. VO is required fields.
------------------------------
Available options:
 --voId        | -v  VO identifier
 --voShortName | -V  VO short name
 --orderById   | -i  order by numeric Id (default)
 --orderByName | -n  order by ShortName
 --batch       | -b  batch
 --help        | -h  prints this help
};
}

my ($voId, $voShortName, $batch, $sortingFunction);
GetOptions("help|h" => sub { print help; exit 0;} ,
           "voId|v=i" => \$voId,
           "voShortName|V=s" => \$voShortName,
           "orderById|i" => sub { $sortingFunction = getSortingFunction("getId") } ,
           "orderByName|n" => sub { $sortingFunction = getSortingFunction("getName", 1) } ,
           "batch|b" => \$batch) || die help;

my $agent = Perun::Agent->new();

#options check
unless(defined $sortingFunction) { $sortingFunction = getSortingFunction("getName", 1); }
unless(defined $voId) {
  unless(defined $voShortName) { die "ERROR: VO specification required.\n"; }
  my $vo = $agent->getVosAgent->getVoByShortName(shortName => $voShortName);
  $voId = $vo->getId;
}

my $resourcesAgent = $agent->getResourcesAgent;
my @resources = $resourcesAgent->getResources(vo => $voId);
unless(@resources) { printMessage "No Resources found", $batch; exit 0; }


#output
my $table = Text::ASCIITable->new();
$table->setCols('ID','Name', 'Facility ID');

foreach my $resource (sort $sortingFunction @resources) {
  my $facility = $resourcesAgent->getFacility(resource => $resource->getId);
  $table->addRow($resource->getId, $resource->getName, $facility->getId);
}

print tableToPrint($table, $batch);
