#!/usr/bin/perl
use feature "switch";
use strict;
use warnings;
use perunServicesInit;
use perunServicesUtils;
use Text::Unidecode;

sub processGroupMembership;

local $::SERVICE_NAME = "ldap";
local $::PROTOCOL_VERSION = "3.0.0";
my $SCRIPT_VERSION = "3.0.2";

perunServicesInit::init;
my $DIRECTORY = perunServicesInit::getDirectory;
my $fileName = "$DIRECTORY/$::SERVICE_NAME".".ldif";
my $baseDnFileName = "$DIRECTORY/baseDN";

my $data = perunServicesInit::getDataWithGroups;

#Constants
our $A_F_BASE_DN;  *A_F_BASE_DN = \'urn:perun:facility:attribute-def:def:ldapBaseDN';
our $A_R_VO_SHORT_NAME;  *A_R_VO_SHORT_NAME = \'urn:perun:resource:attribute-def:virt:voShortName';
our $A_G_NAME;  *A_G_NAME = \'urn:perun:group:attribute-def:core:name';

# User attributes
our $A_USER_LOGIN; *A_USER_LOGIN = \'urn:perun:user_facility:attribute-def:virt:login';
our $A_FIRST_NAME;  *A_FIRST_NAME = \'urn:perun:user:attribute-def:core:firstName';
our $A_LAST_NAME;  *A_LAST_NAME = \'urn:perun:user:attribute-def:core:lastName';
our $A_DISPLAY_NAME;  *A_DISPLAY_NAME = \'urn:perun:user:attribute-def:core:displayName';
our $A_ORGANIZATION;  *A_ORGANIZATION = \'urn:perun:user:attribute-def:def:organization';
our $A_MAIL;  *A_MAIL = \'urn:perun:user:attribute-def:def:preferredMail';

# Member-specific attributes
our $A_M_ORGANIZATION;  *A_M_ORGANIZATION = \'urn:perun:member:attribute-def:def:organization';
our $A_M_MAIL;  *A_M_MAIL = \'urn:perun:member:attribute-def:def:mail';

# CHECK ON FACILITY ATTRIBUTES
my %facilityAttributes = attributesToHash $data->getAttributes;
if (!defined($facilityAttributes{$A_F_BASE_DN})) {
	exit 1;
}

# GATHER USERS
my $users;  # $users->{$login}->{ATTR} = $attrValue;
my $usersByVo;  # $usersByVo->{$voShortName}->{$login}->{ATTR} = $attrValue;

# GATHER GROUPS
my $groups;  # $groups->{$login}->{$voShortName . ":" . $groupName} = 1;
my $groupsByVo;  # $groupsByVo->{$login}->{$voShortName}->{$voShortName . ":" . $groupName} = 1;

my @resourcesData = $data->getChildElements;

# FOR EACH RESOURCE
foreach my $rData (@resourcesData) {
	my %rAttributes = attributesToHash $rData->getAttributes;
	my $voShortName = $rAttributes{$A_R_VO_SHORT_NAME};
	# process members
	my @membersData = ($rData->getChildElements)[1]->getChildElements;

	# FOR EACH MEMBER ON RESOURCE
	foreach my $mData (@membersData) {
		my %mAttributes = attributesToHash $mData->getAttributes;
		my $login = $mAttributes{$A_USER_LOGIN};

		# Store member in VOs structure (use MEMBER attributes)
		$usersByVo->{$voShortName}->{$login}->{$A_DISPLAY_NAME} = $mAttributes{$A_DISPLAY_NAME};
		$usersByVo->{$voShortName}->{$login}->{$A_FIRST_NAME} = $mAttributes{$A_FIRST_NAME};
		$usersByVo->{$voShortName}->{$login}->{$A_LAST_NAME} = $mAttributes{$A_LAST_NAME};

		# if MEMBER variant for ORGANIZATION not defined, try to use USER variant
		my $organization = $mAttributes{$A_M_ORGANIZATION};
		if (defined $organization and length $organization) {
			$usersByVo->{$voShortName}->{$login}->{$A_M_ORGANIZATION} = $organization;
		} else {
			$usersByVo->{$voShortName}->{$login}->{$A_M_ORGANIZATION} = $mAttributes{$A_ORGANIZATION};
		}
		# if MEMBER variant for MAIL not defined, try to use USER variant
		my $mail = $mAttributes{$A_M_MAIL};
		if (defined $mail and length $mail) {
			$usersByVo->{$voShortName}->{$login}->{$A_M_MAIL} = $mail;
		} else {
			$usersByVo->{$voShortName}->{$login}->{$A_M_MAIL} = $mAttributes{$A_MAIL};
		}

		# Store same member in flat structure (use USER attributes)
		$users->{$login}->{$A_DISPLAY_NAME} = $mAttributes{$A_DISPLAY_NAME};
		$users->{$login}->{$A_FIRST_NAME} = $mAttributes{$A_FIRST_NAME};
		$users->{$login}->{$A_LAST_NAME} = $mAttributes{$A_LAST_NAME};
		$users->{$login}->{$A_ORGANIZATION} = $mAttributes{$A_ORGANIZATION};
		$users->{$login}->{$A_MAIL} = $mAttributes{$A_MAIL};

	}

	my @groupsToProcess = ($rData->getChildElements)[0]->getChildElements;
	processGroupMembership( @groupsToProcess , $voShortName );

}

#
# PRINT BASE_DN FILE
#
open FILE,">:encoding(UTF-8)","$baseDnFileName" or die "Cannot open $baseDnFileName: $! \n";
print FILE $facilityAttributes{$A_F_BASE_DN};
close(FILE);

#
# PRINT LDIF FILE
#
open FILE,">:encoding(UTF-8)","$fileName" or die "Cannot open $fileName: $! \n";

# print base entry

print FILE "dn: " . $facilityAttributes{$A_F_BASE_DN} . "\n";
# IF base start with dc=[something]
if ($facilityAttributes{$A_F_BASE_DN} =~ m/^dc=/) {

	my $position = index($facilityAttributes{$A_F_BASE_DN}, ",");
	print $facilityAttributes{$A_F_BASE_DN} . "\n";
	print $position . "\n";
	my $dc = undef;
	if ($position > 0) {
		$dc = substr $facilityAttributes{$A_F_BASE_DN}, 3, $position-3;
	}
	print FILE "dc: " . $dc . "\n";
	print FILE "objectclass: top\n";
	print FILE "objectclass: domain\n";  # we need at least one structural class
	print FILE "objectclass: dcObject\n";
}
# IF base start with ou=[something]
if ($facilityAttributes{$A_F_BASE_DN} =~ m/^ou=/) {

	my $position = index($facilityAttributes{$A_F_BASE_DN}, ",");
	my $ou = undef;
	if ($position > 0) {
		$ou = substr($facilityAttributes{$A_F_BASE_DN}, 3, $position-3);
	}
	print FILE "ou: " . $ou . "\n";
	print FILE "objectclass: organizationalunit\n"
}

print FILE "\n";

# PRINT ou=people entry
print FILE "dn: ou=people," . $facilityAttributes{$A_F_BASE_DN} . "\n";
print FILE "ou: people\n";
print FILE "description: All users propagated from Perun to this LDAP.\n";
print FILE "objectclass: organizationalunit\n";
# There MUST be an empty line after each entry, so entry sorting and diff works on slave part
print FILE "\n";

# FLAT structure is stored in ou=people + base DN
for my $login (sort keys %$users) {

	# print attributes, which are never empty
	print FILE "dn: uid=" . $login . ",ou=people," . $facilityAttributes{$A_F_BASE_DN} . "\n";
	print FILE "uid: " . $login . "\n";
	print FILE "cn: " . $users->{$login}->{$A_DISPLAY_NAME} . "\n";
	print FILE "sn: " . $users->{$login}->{$A_LAST_NAME} . "\n";

	# skip attributes which are empty and LDAP can't handle it (FIRST_NAME, ORGANIZATION, EMAIL)
	my $givenName = $users->{$login}->{$A_FIRST_NAME};
	my $organization = $users->{$login}->{$A_ORGANIZATION};
	my $mail = $users->{$login}->{$A_MAIL};

	if (defined $givenName and length $givenName) {
		print FILE "givenName: " . $givenName . "\n";
	}
	if (defined $organization and length $organization) {
		print FILE "o: " . $organization . "\n";
	}
	if (defined $mail and length $mail) {
		print FILE "mail: " . $mail . "\n";
	}

	# print group membership information
	my @gs = keys %{$groups->{$login}};
	for my $g (@gs) {
		print FILE "memberOfPerunGroups: " . $g . "\n";
	}

	# print classes
	print FILE "objectclass: top\n";
	print FILE "objectclass: inetOrgPerson\n";
	print FILE "objectclass: perunEduroamUser\n";

	# There MUST be an empty line after each entry, so entry sorting and diff works on slave part
	print FILE "\n";

}

# HIERARCHICAL structure is stored in
# base DN (for group entries ou=voShortName)
# ou=voShortName + base DN (for user entries)
for my $voShortName (sort keys %$usersByVo) {

	# PRINT VO entry (create voShortName group in LDAP if not exists)
	print FILE "dn: ou=" . $voShortName . "," . $facilityAttributes{$A_F_BASE_DN} . "\n";
	print FILE "ou: " . $voShortName. "\n";
	print FILE "description: Virtual organization " . $voShortName . " contains all it's members, which are propagated from Perun to this LDAP\n";
	print FILE "objectclass: organizationalunit\n";

	# There MUST be an empty line after each entry, so entry sorting and diff works on slave part
	print FILE "\n";

	# PRINT USERs entries
	my $voUsers = $usersByVo->{$voShortName};
	for my $login (sort keys %$voUsers) {

		# HIERARCHICAL structure is stored in ou=voShortName
		print FILE "dn: uid=" . $login . ",ou=".$voShortName."," . $facilityAttributes{$A_F_BASE_DN} . "\n";
		print FILE "uid: " . $login . "\n";
		print FILE "cn: " . $voUsers->{$login}->{$A_DISPLAY_NAME} . "\n";
		print FILE "sn: " . $voUsers->{$login}->{$A_LAST_NAME} . "\n";

		# skip attributes which are empty and LDAP can't handle it (FIRST_NAME, ORGANIZATION, EMAIL)
		# fallback to user variant is done in aggregation part of the script !!
		my $givenName = $voUsers->{$login}->{$A_FIRST_NAME};
		my $organization = $voUsers->{$login}->{$A_M_ORGANIZATION};
		my $mail = $voUsers->{$login}->{$A_M_MAIL};

		if (defined $givenName and length $givenName) {
			print FILE "givenName: " . $givenName . "\n";
		}
		if (defined $organization and length $organization) {
			print FILE "o: " . $organization . "\n";
		}
		if (defined $mail and length $mail) {
			print FILE "mail: " . $mail . "\n";
		}

		# print group membership information
		my @gs = keys %{$groupsByVo->{$login}->{$voShortName}};
		for my $g (@gs) {
			print FILE "memberOfPerunGroups: " . $g . "\n";
		}

		# print classes
		print FILE "objectclass: top\n";
		print FILE "objectclass: inetOrgPerson\n";
		print FILE "objectclass: perunEduroamUser\n";

		# There MUST be an empty line after each entry, so entry sorting and diff works on slave part
		print FILE "\n";

	}

}

close(FILE);

perunServicesInit::finalize;


#
# Method for gathering group membership information for each user
#
# @param1 = array of Groups assigned to each resource
# @param2 = vo short name (belonging to resource)
#
sub processGroupMembership {

	# read input
	my $voShortName = pop @_;
	my @resGroups = @_;

	# process groups structure
	for my $group (@resGroups) {

		# get group name as attribute
		my %gAttributes = attributesToHash $group->getAttributes;
		my $groupName = $gAttributes{$A_G_NAME};

		# process member data for each group
		my @membersData = ($group->getChildElements)[1]->getChildElements;
		# for each member, push groupName to storage
		foreach my $mData (@membersData) {
			# get login
			my %mAttributes = attributesToHash $mData->getAttributes;
			my $login = $mAttributes{$A_USER_LOGIN};
			# store groupName
			$groupsByVo->{$login}->{$voShortName}->{$voShortName . ":" . $groupName} = 1;
			$groups->{$login}->{$voShortName . ":" . $groupName} = 1;

		}

		# process recursively for each sub-group in this group
		processGroupMembership(($group->getChildElements)[0]->getChildElements , $voShortName);

	}

}

# we do not support EPPN in initial version
#our $A_F_EPPN_SCOPE;  *A_F_EPPN_SCOPE = \'urn:perun:facility:attribute-def:def:eppnScope';
#our $A_F_EPPN_O_BASE_DN;  *A_F_EPPN_O_BASE_DN = \'urn:perun:facility:attribute-def:def:eppnOrgDNBase';
#print FILE "eduPersonPrincipalName: " . $memberAttributes->{$A_USER_LOGIN} . "\@" . $facilityAttributes{$A_F_EPPN_SCOPE} . "\n";
#print FILE "eduPersonAffiliation: member\n";
#print FILE "eduPersonOrgDN: o=" . $memberAttributes->{$A_ORGANIZATION} . "," . $facilityAttributes{$A_F_EPPN_O_BASE_DN} . "\n";
